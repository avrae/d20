// Dice rolling grammar - Avrae spec

expr: num
commented_expr: num COMMENT?
// ^ starting node for commented rolls

// comments are given -1 priority - only match comment if no other possibilities
COMMENT.-1: _WS? /.+/

// math and operators, PMDAS
?num: comparison

?comparison: (comparison COMP_OPERATOR _WS?)? a_num _WS?
COMP_OPERATOR: "==" | ">=" | "<=" | "!=" | "<" | ">"

?a_num: (a_num A_OP _WS?)? m_num _WS?
A_OP: "+" | "-"

?m_num: (m_num M_OP _WS?)? u_num _WS?
M_OP: "*" | "//" | "/" | "%"

?u_num: numexpr | U_OP _WS? u_num
U_OP: "+" | "-"

// numbers
?numexpr: (dice | set | literal) _WS? ANNOTATION*

ANNOTATION: /\[.*?\]/ _WS?

literal: INTEGER | DECIMAL

// sets
?set: setexpr set_op*

set_op: SET_OPERATOR selector
SET_OPERATOR: "k" | "p"

setexpr: "(" _WS? (num (_WS? "," _WS? num)* _WS? comma? _WS?)? _WS? ")"
comma: ","

// dice
?dice: diceexpr dice_op*

dice_op: (DICE_OPERATOR | SET_OPERATOR) selector
DICE_OPERATOR: "rr" | "ro" | "ra" | "e" | "mi" | "ma"

diceexpr: INTEGER? "d" INTEGER

selector: [SELTYPE] INTEGER

SELTYPE: "l" | "h" | "<" | ">"

// whitespace
_WS: /[ \t\f\r\n]/+

// useful constants
%import common.INT -> INTEGER
%import common.DECIMAL
